plugins {
    id "org.springframework.boot" version "2.6.1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "application"
    id "jacoco"
    id "java"
}

java {
    withJavadocJar()
    withSourcesJar()
}

ext {
    set("mapstructVersion", "1.4.2.Final")
    set("lombokMapstructBinding", "0.2.0")
    set("commonsCollections", "4.4")
    set("openApiVersion", "1.5.12")
    set("hamcrestVersion", "1.3")
}

group = "com.ipaixao.ibeer"
version = "0.1.0"

sourceCompatibility = JavaVersion.VERSION_17

applicationName = "i-beer"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springdoc:springdoc-openapi-ui:${openApiVersion}"

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    implementation "org.apache.commons:commons-collections4:${commonsCollections}"
    implementation "org.mariadb.jdbc:mariadb-java-client"
    implementation "org.liquibase:liquibase-core"
    implementation "org.yaml:snakeyaml"

    compileOnly "org.projectlombok:lombok"

    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBinding}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok"

    runtimeOnly "io.micrometer:micrometer-registry-prometheus"
    runtimeOnly "mysql:mysql-connector-java"
    runtimeOnly "com.h2database:h2"

    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.projectlombok:lombok"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.mockito:mockito-core"
}

bootBuildImage {
    environment = [
            "BP_JVM_VERSION": "17.*"
    ]
    imageName = "ipaixao/${applicationName}:${project.version}"
}

application {
    mainClassName = "com.ipaixao.ibeer.IBeerApplication"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
    }
}